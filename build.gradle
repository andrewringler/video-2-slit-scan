plugins {
  id 'java'
  id "edu.sc.seis.macAppBundle" version "2.2.1"
  id "edu.sc.seis.launch4j" version "2.4.3"
  id 'application'
}

apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
applicationDefaultJvmArgs = ["-Xmx2048M"]
mainClassName = "com.andrewringler.slitscan.Video2SlitScan"

group = 'com.andrewringler.slitscan'
version = '0.2.5'

repositories {
    flatDir {
        dirs 'lib', 'lib/p3', 'lib/controlP5', 'lib/jcodec', 'lib/ffmpeg-natives'
    }
 	maven {
        url "http://dev.loci.wisc.edu/maven2/releases/"
    }
 	mavenCentral()
 	jcenter()
 	maven {
        url "https://maven.scijava.org/content/repositories/releases/"
    }
 	maven {
        url "http://maven.imagej.net/content/repositories/releases/"
    }
 	maven {
        url "http://repo.boundlessgeo.com/main/"
    }
}

dependencies {
    compile ":core:@jar",
            ":gluegen-rt:@jar",
            ":jogl-all@jar",
            ":controlP5:@jar",
            "com.google.guava:guava:20.0",
            "org.slf4j:slf4j-api:1.6.1",
            "io.scif:scifio:0.32.0",
            'uk.co.caprica:vlcj:4.4.4',
            ':jcodec-0.2.6-SNAPSHOT@jar',
            ':jcodec-javase-0.2.6-SNAPSHOT@jar',
            'com.github.kokorin.jaffree:jaffree:0.9.5',
            'com.github.javadev:underscore:1.59'
            
    runtime "ch.qos.logback:logback-classic:1.0.9",
            "io.scif:scifio:0.32.0",
            'uk.co.caprica:vlcj-natives:4.1.0',            
            fileTree(dir: 'lib/p3', include: '**/*.jar')
}


//
// Mac
//
// gradle createDmg
//
macAppBundle {
    mainClassName = "com.andrewringler.slitscan.Video2SlitScan"
    appName = "Video-2-Slit-Scan"
    bundleExecutable = "Video-2-Slit-Scan"
    icon = "doc/icon.icns"
    bundleJRE = true
    jreHome = System.getenv('JAVA_HOME')
    javaProperties.put("apple.laf.useScreenMenuBar", "false")
    backgroundImage = "doc/macbackground.png"
    backgroundImageWidth = 527
    backgroundImageHeight = 429
}
task copyDataDirMac(type: Copy) {
	outputs.upToDateWhen { false }
	
	from 'data'
	into "${project.buildDir}/${macAppBundle.appOutputDir}/${macAppBundle.appName}.app/Contents/Java/data"
}
createApp.finalizedBy copyDataDirMac


//
// Windows
//
// gradle createExe
//
launch4j {
    mainClassName = "com.andrewringler.slitscan.Video2SlitScan"
    icon = "${projectDir}/doc/icon.ico"
    windowTitle = "Video-2-Slit-Scan"
}
task copySlitScanNativesWindows(type: Copy) {
	outputs.upToDateWhen { false }
	
	from 'lib/p3-video'
	into "${-> project.buildDir}/${-> launch4j.outputDir}/${-> launch4j.libraryDir}"
}
task copyDataDirWindows(type: Copy) {
	outputs.upToDateWhen { false }
	
	from 'data'
	into "${-> project.buildDir}/${-> launch4j.outputDir}/data"
}
task zipUpWindows(type: Zip) {
	outputs.upToDateWhen { false }

	from "${-> project.buildDir}/${-> launch4j.outputDir}"
   	archiveName "${-> project.name}-${-> project.version}-windows.zip" 
}
zipUpWindows.dependsOn copySlitScanNativesWindows
zipUpWindows.dependsOn copyDataDirWindows
createExe.finalizedBy zipUpWindows


//
// Linux
//
// gradle distTar
//
// for Application/Distributions plugin
// this is the script only version that should work on Mac/Linux/Win
// but currently this is the only version we release for Linux
// Mac and Win have their nicer custom versions with icons and packages
//
distributions {
    main {
        contents {
            from('data') {
                into 'data'
            }
        }
    }
}
tasks.withType(Tar){
    compression = Compression.GZIP
}
