plugins {
  id 'java'
  id "edu.sc.seis.macAppBundle" version "2.2.1"
  id "edu.sc.seis.launch4j" version "2.4.3"
  id 'application'
}

macAppBundle {
    mainClassName = "com.andrewringler.slitscan.Video2SlitScan"
    icon = "doc/icon.icns"
    bundleJRE = true
    javaProperties.put("apple.laf.useScreenMenuBar", "false")
    backgroundImage = "doc/macbackground.png"
    backgroundImageWidth = 527
    backgroundImageHeight = 429
}
launch4j {
    mainClassName = "com.andrewringler.slitscan.Video2SlitScan"
    icon = "${projectDir}/doc/icon.ico"
    windowTitle = "Video-2-Slit-Scan"
}

apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
applicationDefaultJvmArgs = ["-Xmx2048M"]
mainClassName = "com.andrewringler.slitscan.Video2SlitScan"

group = 'com.andrewringler.slitscan'
version = '0.2.3'

repositories {
    flatDir {
        dirs 'lib', 'lib/p3', 'lib/p3-video', 'lib/controlP5'
    }
 	mavenCentral()
  	jcenter()
 	maven {
        url "http://maven.imagej.net/content/repositories/releases/"
    }
 	maven {
        url "http://repo.boundlessgeo.com/main/"
    }
}

dependencies {
    compile ":core:@jar",
            ":gluegen-rt:@jar",
            ":jogl-all@jar",
            ":gstreamer-java:@jar",
            ":jna:@jar",
            ":video:@jar",
            ":controlP5:@jar",
            'com.google.guava:guava:20.0',
            'org.slf4j:slf4j-api:1.6.1',
            'io.scif:scifio:0.32.0'
            
    runtime 'ch.qos.logback:logback-classic:1.0.9',
            'io.scif:scifio:0.32.0',
            fileTree(dir: 'lib/p3', include: '**/*.jar')
}

// the p3-video jars require us to preserve the specific folder structure
// wrap copy in task for delayed evaluation
task copySlitScanNatives {
	outputs.upToDateWhen { false }
	
	doLast {
		Copy myCopy = task(myCopy, type: Copy)
		myCopy.from 'lib/p3-video'
		myCopy.into "${-> project.buildDir}/${-> macAppBundle.appOutputDir}/${-> macAppBundle.appName}.app/Contents/Java"
		myCopy.execute()
	}
}
createApp.finalizedBy copySlitScanNatives

task zipUpWindows(type: Zip) {
   from "${-> project.buildDir}/${-> launch4j.outputDir}"
   include '*'
   include '*/*'
   archiveName "${-> project.name}-${-> project.version}-windows.zip" 
}
task copySlitScanNativesWindows {
	outputs.upToDateWhen { false }
	
	doLast {
		Copy myCopy = task(myCopy, type: Copy)
		myCopy.from 'lib/p3-video'
		myCopy.into "${-> project.buildDir}/${-> launch4j.outputDir}/${-> launch4j.libraryDir}"
		myCopy.execute()
		
		zipUpWindows.execute()
	}
}
createExe.finalizedBy copySlitScanNativesWindows

// for Application plugin (Linux and script for version for Win/Mac)
distributions {
    main {
        contents {
            from(file('lib/p3-video')) {
                into 'lib'
            }
            from(file('data')) {
                into 'bin/data'
            }
        }
    }
}
